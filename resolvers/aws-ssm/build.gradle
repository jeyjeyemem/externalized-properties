description = 'Externalized Properties AWS SSM property resolver.'

dependencies {
    implementation project(':core')
    implementation platform('software.amazon.awssdk:bom:2.17.153')
    implementation 'software.amazon.awssdk:ssm'
}

jar {
    manifest {
        attributes(
            'Automatic-Module-Name': 'io.github.jeyjeyemem.externalizedproperties.resolvers.awsssm'
        )
    }
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project
                implementation project(':core')
                // implementation platform('org.testcontainers:testcontainers-bom:1.16.3')
                implementation 'org.testcontainers:junit-jupiter:1.16.3'
                implementation 'org.testcontainers:localstack:1.16.3'
                // Testcontainers localstack still depends on AWS SDK v1.
                // https://github.com/testcontainers/testcontainers-java/issues/1442#issuecomment-694342883
                implementation "com.amazonaws:aws-java-sdk-s3:1.12.181"
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter test
                    }
                }
            }
        }
    }
}